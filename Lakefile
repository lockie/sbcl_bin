#|-*- mode:lisp -*-|#
(ql:quickload '(:dexador :lake :plump :cl-ppcre :split-sequence :sn.github) :silent t)
(ros:include "util")

(in-package :cl-user)
(defpackage :lake.user
  (:use :cl :lake :cl-syntax :sn.github.repos.releases)
  (:shadowing-import-from :lake
                          :directory))
(in-package :lake.user)

(use-syntax :interpol)

(defvar *user/repo*
  (last (split-sequence:split-sequence
         #\/
         (string-trim
          (string #\lf)
          (uiop:run-program "git remote -v | head -n 1|awk -F ' ' '{print $2}'" :output :string)))
        2))

(defvar *release* "files")
(defvar *user* (first *user/repo*))
(defvar *repo* (subseq (second *user/repo*) 0 (- (length (second *user/repo*)) 4)))

(defvar *release-list-cache* (make-hash-table :test 'equal))
(defvar *ci-supported*
  '("x86-64-linux-binary.tar.bz2"
    "x86-linux-binary.tar.bz2"
    "x86-64-darwin-binary.tar.bz2"
    "x86-darwin-binary.tar.bz2"))

(defun release-exist-p (tagname &key owner repo)
  (ignore-errors
    (find tagname
          (releases-list owner repo)
          :key (lambda (x) (getf x :|tag_name|))
          :test 'equal)))

(defun ensure-release-exists (tagname &key owner repo)
  (let ((found (release-exist-p tagname :owner owner :repo repo)))
    (if found
        found
        (releases-create owner repo tagname))))

(defun asset-upload (path tagname &key owner repo force)
  (let ((release-id (getf (release-exist-p tagname :owner owner :repo repo) :|id|))
        (name (file-namestring path)))
    (format t "release-id: ~A :name ~A force ~A" release-id name force)
    (force-output)
    (when force
      (let ((id (getf (find name (releases-assets-list owner repo release-id)
                            :key (lambda (x) (getf x :|name|))
                            :test #'equal) :|id|)))
        (format t "id:~A" id)
        (force-output)
        (when id 
          (releases-asset-delete owner repo id))))
    (releases-asset-upload
     owner repo
     release-id path)))

(defun github (path tagname owner repo &optional force)
  (unless (uiop:getenv "GITHUB_OAUTH_TOKEN")
    (error "GITHUB_OAUTH_TOKEN must be set"))
  (ensure-release-exists tagname :owner owner :repo repo)
  (when force
    (format t "upload start:"))
  (asset-upload (pathname path) tagname :owner owner :repo repo :force (when force t))
  (when force
    (format t "upload done"))
  t)

(defun output-html (in out &optional version)
  (let ((p (plump:parse (pathname in)))
        (uri (format nil "https://github.com/~A/~A/releases/download/" *user* *repo*)))
    (with-open-file (o (pathname out) :direction :output)
      (when version
        (let ((tmp (format nil "~A~A/sbcl-~A-" uri version version)))
          (mapc
           (lambda (x)
             (format o "<a href=~S></a>~%" (format nil "~A~A" tmp x)))
           *ci-supported*)))
      (mapc #'(lambda (x)
                (let ((file (first (last (split-sequence:split-sequence #\/ (plump:get-attribute x "href"))))))
                  (format o "<a href=~S></a>~%" (concatenate 'string uri (second (split-sequence:split-sequence #\- file)) "/" file))))
            (remove-if-not
             #'(lambda (x)
                 (find (first (last (split-sequence:split-sequence #\.(plump:get-attribute x "href"))))
                       '("bz2" "msi") :test 'equal))
             (plump:get-elements-by-tag-name p "a"))))))

(defun get-tags-from-sourceforge ()
  (let ((html (plump:parse (dex:get "https://sourceforge.net/projects/sbcl/files/sbcl/"))))
    (loop for i in (remove-if-not (lambda (x) (equal (string-trim " " (plump:get-attribute x "class")) "folder"))
                                  (plump:get-elements-by-tag-name html "tr"))
          collect `(,(plump:get-attribute i "title")
                    ,(plump:get-attribute (first (plump:get-elements-by-tag-name i "abbr")) "title")))))

(defun commit-tags (tags)
  (dolist (i tags)
    (dolist (x (list
                (format nil "echo ~A> version" (first i))
                (format nil "git add version")
                (format nil "git commit -m 'version ~A on ~A'" (first i) (second i))
                (format nil "git tag -a \"~A\" -m \"~A\"" (first i) (first i))))
      (uiop:run-program x))))

(defun fetch-upload (path uri)
  "fetch&upload"
  (format t "~A " uri)
  (ignore-errors
   (dex:fetch uri path :if-exists :supersede)
   (let ((*release* (second (split-sequence:split-sequence #\- (pathname-name path)))))
     (github path *release* *user* *repo*)))
  (format t "~%")
  (force-output))

(defun mirror-version (version)
  (loop with base-uri = (format nil "https://sourceforge.net/projects/sbcl/files/sbcl/~A/" version)
        with html = (plump:parse (dex:get base-uri))
        with *release* = version
        for i in (remove-if-not (lambda (x) (equal (string-trim " " (plump:get-attribute x "class")) "file"))
                                (plump:get-elements-by-tag-name html "tr"))
        for uri = (concatenate 'string base-uri (plump:get-attribute i "title"))
        for path = (file-namestring (quri:uri-path (quri:uri uri)))
        do (fetch-upload path uri)))

(defun mirror-newest ()
  (dex:fetch "http://sbcl.org/platform-table.html" #P"sbcl-bin.html" :if-exists :supersede)
  (sh "cat sbcl-bin.html| grep http|awk -F '\"' '{print $2}'|grep binary > uris")
  (with-open-file (in "uris")
    (loop for uri = (read-line in nil nil)
          while uri
          for path = (file-namestring (quri:uri-path (quri:uri uri)))
          do (fetch-upload path uri)))
  (let ((path "sbcl-bin.html")
        (out "mirror.html"))
    (format t "~A ~%" (list path *release* *user* *repo* t))
    (force-output)
    (ignore-errors
     (github path *release* *user* *repo* t))
    (output-html path out)
    (ignore-errors
      (github out *release* *user* *repo* t))))

(defun build-sbcl-bin (version &optional (uname-m (ros.util:uname-m)))
  (let ((release-dir (namestring (uiop:getcwd))))
    (sh "rm -rf sbcl")
    (sh (format nil "git clone --depth 5 https://github.com/sbcl/sbcl --branch=sbcl-~A" version))
    (uiop:chdir "sbcl")
    (sh "rm -rf .git")
    (sh (format nil "echo '~S' > version.lisp-expr " version))
    (sh (format nil "bash make.sh --with-sb-thread --with-sb-core-compression --arch=~A '--xc-host=ros -L sbcl-bin run'"
                uname-m))
    (sh "bash run-sbcl.sh --eval '(progn (print *features*)(quit))'")
    (uiop:chdir release-dir)
    (sh (format nil "ln -s sbcl ~Asbcl-~A-~A-~A"
                release-dir
                version
                uname-m
                (ros.util:uname)))
    (sh (format nil "sbcl/binary-distribution.sh sbcl-~A-~A-~A"
                version
                uname-m
                (ros.util:uname)))
    (let ((path (format nil "sbcl-~A-~A-~A-binary.tar"
                        version
                        uname-m
                        (ros.util:uname))))
      (sh (format nil "rm -f ~A.bz2" path))
      (sh (format nil "bzip2 ~A" path))
      (loop repeat 10
            until (ignore-errors
                   (github (format nil "~A.bz2" path) version *user* *repo*))
            do (sleep 3)))))

(task "default" ()
  (let ((ver (or (unless (zerop (length (ros:getenv "TRAVIS_TAG")))
                   (ros:getenv "TRAVIS_TAG"))
                 (unless (zerop (length (ros:getenv "VERSION")))
                   (ros:getenv "VERSION"))))
        (m (ros:getenv "TARGET")))
    (if ver
        (progn
          #-darwin
          (when (equal "x86-64" m)
            (mirror-version ver))
          (build-sbcl-bin ver m)
          #+darwin
          (when (equal "x86" m)
            (let ((path "sbcl-bin.html")
                  (out "build.html"))
              (dex:fetch "http://sbcl.org/platform-table.html"
                         #P"sbcl-bin.html" :if-exists :supersede)
              (format t "~A ~%" (list path *release* *user* *repo* t))
              (force-output)
              (output-html path out ver)
              (ignore-errors
                (github out *release* *user* *repo* t)))))
        #-darwin
        (mirror-newest))))
