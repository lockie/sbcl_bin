#|-*- mode:lisp -*-|#
(load "roswell.github.utils.asd")
(ql:quickload '(:roswell.github.utils #-lparallel :cl-syntax :jonathan) :silent t)
(ros:include "util")

(in-package :cl-user)
(defpackage :lake.user
  (:use :cl #+lparallel :lake :cl-syntax :roswell.github.utils)
  #+lparallel
  (:shadowing-import-from :lake
                          :directory))
(in-package :lake.user)
(use-syntax :interpol)

(defvar *ci-supported*
  '("x86-64-linux-binary.tar.bz2"
    "x86-linux-binary.tar.bz2"
    "x86-64-darwin-binary.tar.bz2"
    "x86-darwin-binary.tar.bz2"
    "arm64-linux-binary.tar.bz2"))

(defvar *tsv-headers*
  '("os""arch""version""variant""uri"))

#-lparallel ;; missing functions from lake
(progn
  (defmacro task (name ign &body body)
    (declare (ignore ign))
    `(defun ,(read-from-string name) ()
       ,@body))
  (defgeneric sh (command &key echo)
    (:documentation
     "Takes a string or list of strings and runs it from a shell."))

  (defmethod sh ((command string) &key echo)
    (when echo
      (format *error-output* "~A" command))
    (multiple-value-bind (output error-output return-status)
        (uiop:run-program command :input :interactive
                          :output :interactive
                          :error-output :interactive
                          :ignore-error-status t)
      (declare (ignore output error-output))
      (unless (zerop return-status)
        (error "Command ~S exited with error code ~A." command return-status))))

  (defmethod sh ((command list) &key echo)
    (let ((command1 (format nil "~{~A~^ ~}" command)))
      (sh command1 :echo echo))))

(defun output-html (in out &optional version)
  (let ((p (plump:parse (pathname in)))
        (uri (format nil "https://github.com/~A/~A/releases/download/" *user* *repo*)))
    (with-open-file (o (pathname out) :direction :output)
      (when version
        (let ((tmp (format nil "~A~A/sbcl-~A-" uri version version)))
          (mapc
           (lambda (x)
             (format o "<a href=~S></a>~%" (format nil "~A~A" tmp x)))
           *ci-supported*)))
      (mapc #'(lambda (x)
                (let ((file (first (last (split-sequence:split-sequence #\/ (plump:get-attribute x "href"))))))
                  (format o "<a href=~S></a>~%" (concatenate 'string uri (second (split-sequence:split-sequence #\- file)) "/" file))))
            (remove-if-not
             #'(lambda (x)
                 (find (first (last (split-sequence:split-sequence #\.(plump:get-attribute x "href"))))
                       '("bz2" "msi") :test 'equal))
             (plump:get-elements-by-tag-name p "a"))))))

(defvar *replace-assoc*
  '(("dragonfly" . "DFlyBSD")
    ("powerpc" . "ppc")
    ("mips" . "mipsbe")))

(defun split-filename (name)
  (labels ((% (l)
             (cond ((null l) nil)
                   ((and
                     (equal (first l) "x86")
                     (equal (second l) "64")
                     (cons "x86-64" (% (cddr l)))))
                   ((and
                     (equal (first l) "debian")
                     (equal (second l) "kfreebsd"))
                    (cons "kFreBSD" (% (cddr l))))
                   ((assoc (first l) *replace-assoc* :test 'equal)
                    (cons (cdr (assoc (first l) *replace-assoc* :test 'equal)) (% (cdr l))))
                   ((string= (first l) "openbsd" :end1 (min (length "openbsd") (length (first l))))
                    (cons "OpenBSD" (% (cddr l))))
                   (t (cons (first l) (% (cdr l)))))))
    (% (uiop:split-string name :separator '(#\-)))))

(defun latest-uris ()
  (loop with html = (dex:get (format nil "https://api.github.com/repos/~A/~A/releases/latest" *user* *repo*))
        for asset in (getf (funcall (read-from-string "jojo:parse") html) :|assets|)
        when (find (getf asset :|content_type|) '("application/x-bzip2" "application/x-msi") :test 'equal)
        collect (getf asset :|browser_download_url|)))

(defun mirror-uris ()
  (loop with uri = (format nil "https://github.com/~A/~A"  *user* *repo*)
        with bin = (dex:get (format nil "~A/releases/download/files/mirror.html" uri) :force-binary t)
        with lines = (babel:octets-to-string bin)
        for line in (uiop:split-string lines :separator '(#\Newline))
        collect (second (uiop:split-string line :separator '(#\")))))

(defun output-updated-tsv (out)
  (flet ((sub (o)
           (format o "~{~A~^	~}~%" *tsv-headers*)
           (loop with latests
                 for uri in (append (latest-uris) (mirror-uris))
                 for file = (first (last (uiop:split-string uri :separator '(#\/))))
                 for sep = (split-filename file)
                 for version = (second sep)
                 for os = (fourth sep)
                 for arch = (third sep)
                 for variant = (if (= (length sep) 5) "" (fifth sep))
                 when (and (find (first (last sep)) '("binary.tar.bz2" "binary.msi") :test 'equal)
                           (not (find (list os arch variant) latests :test 'equalp)))
                 do (push (list os arch variant) latests)
                    (format o "~{~A~^	~}~%" `(,os ,arch ,version ,variant ,uri)))))
    (cond ((eql out t)
           (sub *standard-output*))
          ((eql out nil)
           (with-output-to-string (o)
             (sub o)))
          (t
           (with-open-file (o (pathname out) :direction :output :if-exists :supersede)
             (sub o))))))

(defun mirror-version (version)
  (loop with releases = (releases-list *user* *repo*)
        with base-uri = (format nil "https://sourceforge.net/projects/sbcl/files/sbcl/~A/" version)
        with html = (progn (format t "get ~A~%" base-uri) (finish-output) (plump:parse (dex:get base-uri)))
        with *release* = version
        for i in (remove-if-not (lambda (x) (equal (string-trim " " (plump:get-attribute x "class")) "file"))
                                (plump:get-elements-by-tag-name html "tr"))
        for uri = (concatenate 'string base-uri (plump:get-attribute i "title"))
        for path = (file-namestring (quri:uri-path (quri:uri uri)))
        do (if (or (find-if (lambda (x) (equal path (format nil "sbcl-~A-~A" version x)))
                            *ci-supported*)
                   (find path (getf (find *release*
                                          releases
                                          :key (lambda (x) (getf x :|tag_name|))
                                          :test 'equal) :|assets|)
                         :key (lambda (x) (getf x :|name|))
                         :test #'equal))
               (format t "skip ~A~%" uri)
               (fetch-upload path uri (second (split-sequence:split-sequence #\- (pathname-name path)))))))

(defun mirror-newest ()
  (format t "mirror-newest~%")(force-output)
  (dex:fetch "http://sbcl.org/platform-table.html" #P"sbcl-bin.html" :if-exists :supersede)
  (sh "cat sbcl-bin.html| grep http|awk -F '\"' '{print $2}'|grep binary > uris")
  (format t "fetching uris~%")(force-output)
  (with-open-file (in "uris")
    (loop with releases = (progn
                            (format t "fetching releases-list ~A ~A" *user* *repo*)(force-output)
                            (prog1 (releases-list *user* *repo*)
                              (format t "done~%")(force-output)))
          for uri = (read-line in nil nil)
          while uri
          for path = (file-namestring (quri:uri-path (quri:uri uri)))
          for *release* = (second (split-sequence:split-sequence #\- (pathname-name path)))
          do (format t "~%~a " uri)
             (force-output)
             (if (find path (getf (find *release*
                                        releases
                                        :key (lambda (x) (getf x :|tag_name|))
                                        :test 'equal) :|assets|)
                       :key (lambda (x) (getf x :|name|))
                       :test #'equal)
                 (format t "skip")
                 (fetch-upload path uri (second (split-sequence:split-sequence #\- (pathname-name path)))))
             (force-output)))
  (let ((path "sbcl-bin.html")
        (out "mirror.html"))
    (format t "~A ~%" (list path *release* *user* *repo* t))
    (force-output)
    (ignore-errors
      (github path *release* *user* *repo* t))
    (output-html path out)
    (ignore-errors
      (github out *release* *user* *repo* t))))

(defun git-clone (uris version release)
  (format nil "~{~A~^ || ~}"
          (mapcar (lambda (uri)
                    (format nil "git clone ~:[--depth 5 ~;~:]~A --branch=~:[master~;sbcl-~:*~A~]"
                            (and version (not release))
                            uri
                            (and release version)))
                  uris)))

(defun build-sbcl-bin (version uname-m suffix)
  (let ((release-dir (namestring (uiop:getcwd)))
        (uname (roswell.util:uname))
        (release (and version (< (length version) 20)))) ;; sha length is 40,version length is about 10.20 is enough.
    (sh "rm -rf sbcl")
    (sh (git-clone '("https://git.code.sf.net/p/sbcl/sbcl"
                     "https://github.com/sbcl/sbcl") version release))
    (uiop:chdir "sbcl")
    (when (and version (not release))
      (sh (format nil "git checkout ~A" version)))
    (sh "rm -rf .git")
    (let ((q #+win32"" #-win32"'"))
      (sh (format nil "echo ~A~S~A > version.lisp-expr" q (if release version "99.99.99") q)))
    (dolist (file (append
                   (and (find version '("1.3.15" "1.3.16") :test 'equal) (find :darwin *features*) (equal uname-m "x86-64")
                        '("darwin-1.3.15-16"))
                   (and (find :win32 *features*)
                        '(;;"win32-posixwait"  applicable until 1.4.12
                          ))))
      (sh (format nil "cat ../patch/~A|patch -p0 -N" file)))
    (ignore-errors
      (sh (format nil "bash make.sh --with-sb-core-compression --arch=~A '--xc-host=ros -L sbcl-bin without-roswell=t --no-rc run'"
                  uname-m)))
    (sh "bash run-sbcl.sh --eval '(progn (print *features*)(quit))'")
    #-win32
    (progn
      (uiop:chdir release-dir)
      (sh (format nil "ln -s sbcl ~Asbcl-~A-~A-~A~A"
                  release-dir
                  version
                  uname-m
                  uname
                  suffix))
      (sh (format nil "sbcl/binary-distribution.sh sbcl-~A-~A-~A~A"
                  version
                  uname-m
                  uname
                  suffix))
      (let ((path (format nil "sbcl-~A-~A-~A~A-binary.tar"
                          version
                          uname-m
                          uname
                          suffix)))
        (sh (format nil "rm -f ~A.bz2" path))
        (sh (format nil "bzip2 ~A" path))))
    #+win32
    (progn
      (sh "bash make-windows-installer.sh")
      (sh "cp output/*.msi .."))))

(defun env (name)
  (let ((val (ros:getenv name)))
    (unless (zerop (length val))
      val)))

(defun version-target ()
  (values (or (env "TRAVIS_TAG")
              (env "APPVEYOR_REPO_TAG_NAME")
              (env "VERSION")
              (let ((branch (env "TRAVIS_BRANCH")))
                (unless (equal "master" branch)
                  branch)))
          (or (env "TARGET")
              (roswell.util:uname-m))
          (or (env "SUFFIX")
              "")))

(defun upload-archive ()
  (multiple-value-bind (version target suffix) (version-target)
    (loop with uname =  (roswell.util:uname)
          with release  = (and version (< (length version) 20))
          for m in (split-sequence:split-sequence #\, target)
          for path = (format nil "sbcl-~A-~A-~A~A-binary"
                             version
                             m
                             uname
                             suffix)
          when release
          do (ql:quickload :sb-md5)
             (let ((bz2 (format nil #-win32 "~A.tar.bz2" #+win32 "~A.msi" path)))
               (format t "~(~{~2,'0X~}~)"
                       (map 'list #'identity (funcall (read-from-string "sb-md5:md5sum-file") bz2)))
               (loop repeat 10
                     until (ignore-errors
                             (github bz2 version *user* *repo* t))
                     do (sleep 3))))))


(task "default" ()
  (multiple-value-bind (ver target suffix) (version-target)
    (loop for m in (split-sequence:split-sequence #\, target)
          do (format t "~S ~S ~S~%" ver m suffix)
             (force-output)
             (build-sbcl-bin ver m suffix))))

(task "upload" ()
  (upload-archive))

(task "mirror" ()
  (let ((ver (version-target)))
    (if ver
        (and (< (length ver) 20)
             (mirror-version ver))
        (mirror-newest)))
  (let ((out "sbcl-bin_uri"))
    (output-updated-tsv out)
    (github out *release* *user* *repo* t)))

(task "upload-build-list" ()
  (multiple-value-bind (ver) (version-target)
    (when (and (not (zerop (length ver)))
               (< (length ver) 20))
      (let ((path "sbcl-bin.html")
            (out "build.html"))
        (dex:fetch "http://sbcl.org/platform-table.html"
                   #P"sbcl-bin.html" :if-exists :supersede)
        (format t "~A ~%" (list path *release* *user* *repo* t))
        (force-output)
        (output-html path out ver)
        (ignore-errors
          (github out *release* *user* *repo* t))))))

(task "tsv" ()
   (output-updated-tsv "sbcl-bin_uri"))

#-lparallel
(unwind-protect
     (let (method)
       (and (setf method (ros:getenv "METHOD"))
            (setf method (read-from-string method))
            (fboundp method)
            (funcall method)))
  (unless (find-package :swank)
    (uiop:quit)))
